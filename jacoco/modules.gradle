apply plugin: 'jacoco'

jacoco {
    toolVersion '0.8.7'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

project.afterEvaluate {

    (android.hasProperty('applicationVariants') ? android.'applicationVariants' : android.'libraryVariants')
            .all { variant ->
                def variantName = variant.name
                def capVariantName = variantName.capitalize()
                def testTaskName = "test${capVariantName}UnitTest"
                tasks.create(name: "${variantName}Coverage", type: JacocoReport, dependsOn: testTaskName) {

                    group = "Reporting"
                    description = "Generate Jacoco coverage reports for the ${variantName} build."

                    reports {
                        xml.required = true
                        csv.required = true
                        html.required = true
                        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
                    }

                    def excludes = [
                            '**/R.class',
                            '**/R$*.class',
                            '**/BuildConfig.*',
                            '**/Event.*',
                            '**/AppConfig.*',
                            '**/EventCacheInfo.*',
                            '**/Location.*',
                            '**/Manifest*.*',
//        '**/*Test*.*',
                            '**/*Log*.*',
                            'android/**/*.*',
                            '**/jdo/*.*',
                            '**/listeners/*.*',
                            '**/compose/components/*.*',
                            '**/compose/components/theme/*.*',
                            '**/compose/*.*',
                            '**/compose/recurring/ui/theme/*.*',
                            '**/Retrofit/*.*',
                            '**/models/*.*',
                            '**/adapter/*.*',
                            '**/fragments/*.*',
                            '**/interface/*.*',
                            '**/factory/*.*',
                            '**/layout/*.*',
                            '**/values/*.*',
                            '**/javascript/*.*',
                            'androidx/**/*.*',
                            '**/com/full/calendarsdk/**/*.*',
                            '**com/anywhere/bookingpolicy/**/*.*',
                            '**/*$ViewInjector*.*',
                            '**/*Dagger*.*',
                            '**/*I.*',
                            '**/*Listener*.*',
//        '**/*Test*.*',
                            '**/*MembersInjector*.*',
                            '**/*_Factory.*',
                            '**/*_Provide*Factory*.*',
                            '**/*_ViewBinding*.*',
                            '**/AutoValue_*.*',
                            '**/R2.class',
                            '**/R2$*.class',
                            '**/*Binding.*',
                            '**/*Preference*.*',
                            '**/*Constant*.*',
                            '**/*Resource*.*',
                            '**/*DAO*.*',
                            '**/externalCalendar/compose/*.*',
                            '**/*Builder*.*',
                            '**/models/*.*',
                            '**/api/*.*',
                            '**/enum/*.*',
                            '**/*SDK*.*',
                            '**/*View.*',
                            '**/*fragment*.*',
                            '**/*SDKException*.*',
                            '**/CalendarControllerFragment.kt.*',
                            '**/res/**/*.*',
                    ]

                    def jClasses = "${project.buildDir}/intermediates/javac/${variantName}/classes"
                    def kClasses = "${project.buildDir}/tmp/kotlin-classes/${variantName}"
                    def javaClasses = fileTree(dir: jClasses, excludes: excludes)

                    def kotlinClasses = fileTree(dir: kClasses, excludes: excludes)

                    classDirectories.from = files([javaClasses, kotlinClasses])
                    def sourceDirs = ["${project.projectDir}/src/main/java", "${project.projectDir}/src/main/kotlin",
                                      "${project.projectDir}/src/${variantName}/java", "${project.projectDir}/src/${variantName}/kotlin"]

                    sourceDirectories.from = files(sourceDirs)

                    executionData.from = files(["${project.buildDir}/jacoco/${testTaskName}.exec"])
                }
            }

    tasks.register('coverAllVariants') {
        dependsOn(provider {
            tasks.findAll { task -> task.name.endsWith('Coverage') }
        })
    }
}
